plugins {
    id 'java'
    id 'idea'
    id 'application'
	id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
	id 'org.openjfx.javafxplugin' version '0.0.8'
}

repositories {
    mavenCentral()
    maven {
        name "Fabric"
        url 'https://maven.fabricmc.net/'
    }
}

sourceCompatibility = 11
targetCompatibility = 11

group = 'matcher'

sourceSets {
    main {
        java.srcDirs = ['src']
		resources.srcDirs = ['res']
    }
}

javafx {
	version = "11.0.2"
	modules = [ 'javafx.controls', 'javafx.web' ]
}

dependencies {
    implementation "com.github.javaparser:javaparser-core:3.8.3"
	implementation "org.bitbucket.mstrobel:procyon-compilertools:0.5.33.6"
	implementation "org.ow2.asm:asm:7.3.1"
	implementation "org.ow2.asm:asm-commons:7.3.1"
	implementation "org.ow2.asm:asm-tree:7.3.1"
	implementation "org.ow2.asm:asm-util:7.3.1"
	implementation "org.benf:cfr:0.149"
	implementation "org.jetbrains:intellij-fernflower:1.0.0.6"
}

application {
    mainClass = 'matcher.Main'
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

compileJava {
	inputs.property("moduleName", projects.name)
	doFirst {
		options.compilerArgs = [
			'--module-path', classpath.asPath,
		]
		classpath = files()
	}
}

build.dependsOn(shadowJar)

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact shadowJar
		}
	}
}
